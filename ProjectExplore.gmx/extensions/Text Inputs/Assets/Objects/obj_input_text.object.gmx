<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>&lt;undefined&gt;</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>0</depth>
  <persistent>0</persistent>
  <parentName>&lt;undefined&gt;</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="1" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>ds_list_destroy(undo_redo_text_list);
ds_list_destroy(undo_redo_cursor_position_list);

if (multiline) ds_list_destroy(new_line_list);

if (selection_enabled) surface_free(sf_selection);

surface_free(sf_main);
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// How the system works:
// When a text ipnut is enabled, it will check for user interaction. Whenever GM's keyboard_string has text, the text is added to the text input and the keyboard_string is cleared. This way keyboard_string controls all
// character input (but the rest is done by the program (even backspace which is usually done by keyboard_string)). Input through keyboard_string is considered one of the 9 "actions":
// None (0), keyboard_string input (1), delete/backspace (2), text navigation (3), enter (4), undoing/redoing (5), cutting (6), pasting (7), text change from outside with input_text_set_text (8).
// Organizing the actions like this is useful for adding states to the undo/redo list; we don't want to add an undo/redo state for every new character, so instead we wait for the current action to
// differ from the previous one (in other words; we save when the action changes). Only one action can happen each step. The first while loop below is responsible for finding the action and start doing changes to the variable
// "text" (which holds all input). After this, if action is not 0, the update of text gets completed, and text is put into text_draw, text wrapped (if multiline), analyzed for line information (if multiline), then all "#" are
// replaced with "\#". text_draw is the string that's drawn to screen in the draw event. We also update the cursor's screen position, and offsets and matrices according to "fit style" to fit the text to its width and height.
// Note that we use chr(10) a.k.a. line feed for new line characters. (We're not using the chr(13) + chr(10) standard as GM's draw_text sees this as two new lines (unless in HTML5).) The if statement in the end is for saving
// undo/redo states and updating the cursor timer. If you have any questions, send me (Dragon47) a message on the Game Maker Community website.

if (enabled) {
    // Holding down certain keys can eventually simulate a series of presses. (E.g. holding down arrow keys when navigating text.)
    key_pressed = 0;
    if (keyboard_check_pressed(vk_anykey)) {key_pressed_timer = 380000; key_down = keyboard_key; key_pressed = key_down;}
    else {key_pressed_timer -= delta_time; if (key_pressed_timer &lt;= 0) {key_pressed_timer = 26000; if (keyboard_check(key_down)) key_pressed = key_down;}}
    
    // Set mouse position variables and apply transformations. You can change mouse_x/y to window_mouse_get_x/y if you want it irrespective of views.
    var m_x = mouse_x - x, m_y = mouse_y - y;
    if (image_angle != 0) {var t_x = m_x, t_y = m_y; m_x = lengthdir_x(t_x, image_angle) + lengthdir_y(t_y, image_angle); m_y = lengthdir_x(t_y, image_angle) - lengthdir_y(t_x, image_angle);}
    if (image_xscale == 0) m_x = 0; else m_x /= image_xscale;
    if (image_yscale == 0) m_y = 0; else m_y /= image_yscale;
}

draw_set_font(font);
var cursor_stay_visible = false, add_to_text = "", action = 0;
if (max_undo_redo_states != 0) var text_previous = text;
if (selection_enabled) var selection_update = false;
    
// Checks actions for moving the cursor or changing text.
while (true) {
    // If input_text_set_text was called.
    if (is_string(text_change_from_outside)) {
        text = text_change_from_outside; text_change_from_outside = false;
        cursor_position = string_length(text) + 1;
        if (selection_enabled) selection_start = 0;
        cursor_stay_visible = true; action = 8; break;
    }
    
    // Further input is only available if the text input is enabled.
    if (!enabled) break;
    
    // Copying/cutting.
    if (selection_enabled &amp;&amp; selection_start != 0 &amp;&amp; keyboard_check(vk_control)) {
        if (key_pressed == ord("C") || key_pressed == ord("X")) {
            var s = string_copy(text, selection_start_ordered, selection_end_ordered - selection_start_ordered);
            if (os_device == os_windows &amp;&amp; os_browser == browser_not_a_browser) clipboard_set_text(s); else global.clipboard_local = s;
            if (key_pressed == ord("X")) {action = 6; break;}
        }
    }
    
    // Undos/redos. (When undoing/redoing, the previous text and cursor position is swapped with the text and cursor position previously saved in the undo/redo lists.)
    if (max_undo_redo_states != 0 &amp;&amp; keyboard_check(vk_control)) {
        if (((keyboard_check(vk_shift) &amp;&amp; key_pressed == ord("Z")) || key_pressed == ord("Y")) &amp;&amp; undo_redo_position &lt; ds_list_size(undo_redo_text_list) - 1) {
            ++undo_redo_position;
            text = undo_redo_text_list[| undo_redo_position]; cursor_position = undo_redo_cursor_position_list[| undo_redo_position];
            if (selection_enabled) selection_start = 0;
            cursor_stay_visible = true; action = 5; break;
        }
        if (!keyboard_check(vk_shift) &amp;&amp; key_pressed == ord("Z") &amp;&amp; undo_redo_position &gt; 0) {
            --undo_redo_position;
            text = undo_redo_text_list[| undo_redo_position]; cursor_position = undo_redo_cursor_position_list[| undo_redo_position];
            if (selection_enabled) selection_start = 0;
            cursor_stay_visible = true; action = 5; break;
        }
    }
    
    // Text navigation with up/down and mouse, and double click/ctrl + A selection.
    if (selection_enabled &amp;&amp; keyboard_check(vk_control) &amp;&amp; key_pressed == ord("A")) {
        selection_start = 1; cursor_position = string_length(text) + 1; selection_update = true;
        action = 3; break;
    }
    if (mouse_check_button_pressed(mb_left) || (selection_enabled &amp;&amp; mouse_check_button(mb_left) &amp;&amp; (m_x != m_x_previous || m_y != m_y_previous))
       || (multiline &amp;&amp; (key_pressed == vk_up || key_pressed == vk_down || key_pressed == vk_pageup || key_pressed == vk_pagedown))) {
        if (selection_enabled) {
            var double_clicked = false;
            if (mouse_check_button_pressed(mb_left)) {
                double_clicked = (enabled_step &gt; 1 &amp;&amp; selection_start == 0 &amp;&amp; m_x == m_x_previous &amp;&amp; m_y == m_y_previous &amp;&amp; get_timer() - mouse_pressed_previous &lt; double_click_time
                                 &amp;&amp; (string_lettersdigits_universal(string_char_at(text, cursor_position)) != "" || string_lettersdigits_universal(string_char_at(text, cursor_position - 1)) != ""));
                mouse_pressed_previous = get_timer();
            }
        }
        if (selection_enabled &amp;&amp; double_clicked) {
            mouse_clear(mb_left);
            if (multiline &amp;&amp; cursor_position &lt; 0) cursor_position = -cursor_position + 1;
            selection_start = cursor_position;
            var char, char_before;
            while (true) {
                char = string_char_at(text, selection_start);
                if (selection_start == 1) char_before = ""; else char_before = string_char_at(text, selection_start - 1);
                if (string_lettersdigits_universal(char) != "" &amp;&amp; string_lettersdigits_universal(char_before) == "") break;
                --selection_start; if (selection_start &lt;= 1) break;
            }
            while (true) {
                char = string_char_at(text, cursor_position);
                if (cursor_position == 1) char_before = ""; else char_before = string_char_at(text, cursor_position - 1);
                if (string_lettersdigits_universal(char) == "" &amp;&amp; string_lettersdigits_universal(char_before) != "") break;
                ++cursor_position; if (cursor_position &gt; string_length(text)) break;
            }
            selection_update = true;
        } else {
            var searched_width = 0, smallest = -1, target_width, last_index, line_width, line;
            if (multiline) {
                if (key_pressed == vk_up) {
                    line = cursor_line - 1;
                    if (keyboard_check(vk_control)) while (line &gt; 0) {
                        if (new_line_list[| line] - 1 == new_line_list[| line - 1] &amp;&amp; cursor_position != new_line_list[| line] + 1 &amp;&amp; string_char_at(text, new_line_list[| line] + 1) != chr(10)) break; --line;
                    }
                } else if (key_pressed == vk_down) {
                    line = cursor_line + 1;
                    if (keyboard_check(vk_control)) while (line &lt; lines - 1) {
                        if (new_line_list[| line] - 1 == new_line_list[| line - 1] &amp;&amp; string_char_at(text, new_line_list[| line] + 1) != chr(10)) break; ++line;
                    }
                } else if (key_pressed == vk_pageup) {
                    if (keyboard_check(vk_control)) {
                        if (v_align == fa_top) line = ceil(-offset_y / line_separation);
                        else if (v_align == fa_bottom) line = ceil(lines - (offset_y + height) / line_separation);
                        else line = ceil(lines * 0.5 - (height * 0.5 + offset_y) / line_separation);
                    } else {line = cursor_line - (height div line_separation); offset_y += (height div line_separation) * line_separation;}
                } else if (key_pressed == vk_pagedown) {
                    if (keyboard_check(vk_control)) {
                        if (v_align == fa_top) line = floor((height - offset_y) / line_separation - 1);
                        else if (v_align == fa_bottom) line = floor(lines - offset_y / line_separation - 1);
                        else line = floor(lines * 0.5 + (height * 0.5 - offset_y) / line_separation - 1);
                    } else {line = cursor_line + (height div line_separation); offset_y -= (height div line_separation) * line_separation;}
                } else line = (m_y - offset_y - v_align_offset) div line_separation;
                line = clamp(line, 0, lines - 1);
                cursor_position = new_line_list[| line] + 1; last_index = min(new_line_list[| line + 1], string_length(text)) + 1;
                line_width = string_width(string_replace_all(string_copy(text, cursor_position, last_index - cursor_position), "#", "\#"));
            } else {cursor_position = 1; last_index = string_length(text) + 1; line_width = text_width;}
            if (multiline &amp;&amp; (key_pressed == vk_up || key_pressed == vk_down)) {if (keyboard_check(vk_control)) target_width = -line_width; else target_width = cursor_offset_x - offset_x;}
            else if (multiline &amp;&amp; (key_pressed == vk_pageup || key_pressed == vk_pagedown)) target_width = cursor_offset_x - offset_x; else target_width = m_x - offset_x;
            if (h_align == fa_center) target_width += (line_width &gt;&gt; 1); else if (h_align == fa_right) target_width += line_width;
            if (target_width &gt; 0) while (true) {
                if (smallest == -1 || abs(target_width - searched_width) &lt;= smallest) smallest = abs(target_width - searched_width); else {--cursor_position; break;}
                if (cursor_position == last_index) break;
                searched_width += string_width(string_replace(string_char_at(text, cursor_position), "#", "\#")); ++cursor_position;
            }
            if (multiline &amp;&amp; cursor_position == last_index &amp;&amp; cursor_position - 1 &lt; string_length(text)) {--cursor_position; if (string_char_at(text, cursor_position) != chr(10)) cursor_position *= -1;}
        }
        m_x_previous = m_x; m_y_previous = m_y;
        action = 3; break;
    }
    
    // Pasting text.
    if (clipboard_pasting_enabled &amp;&amp; keyboard_check(vk_control) &amp;&amp; key_pressed == ord("V") &amp;&amp; (os_device != os_windows || os_browser != browser_not_a_browser || clipboard_has_text())) {
        var s; if (os_device == os_windows &amp;&amp; os_browser == browser_not_a_browser) s = clipboard_get_text(); else s = global.clipboard_local;
        add_to_text = string_replace_all(s, chr(13), "");
        if (!multiline) add_to_text = string_replace_all(add_to_text, chr(10), "");
        action = 7; break;
    }
    
    // Adding new lines.
    if (multiline &amp;&amp; key_pressed == vk_enter) {add_to_text = chr(10); action = 4; break;}
    
    // Delete and backspace.
    if (key_pressed == vk_delete) {
        if (selection_enabled &amp;&amp; selection_start != 0) {action = 2; break;}
        if (cursor_position &lt;= string_length(text)) {
            var position = cursor_position;
            if (keyboard_check(vk_control))
                while (position &lt;= string_length(text)) {
                    var char = string_char_at(text, position), char_next = string_char_at(text, position + 1);
                    if (char == chr(10) || char_next == chr(10) || (string_lettersdigits_universal(char) == "" &amp;&amp; string_lettersdigits_universal(char_next) != "")) break;
                    ++position;
                }
            text = string_delete(text, cursor_position, position - cursor_position + 1);
            cursor_stay_visible = true; action = 2; break;
        }
    }
    if (key_pressed == vk_backspace) {
        if (selection_enabled &amp;&amp; selection_start != 0) {action = 2; break;}
        if (cursor_position &gt; 1) {
            var position = cursor_position - 1;
            if (keyboard_check(vk_control))
                while (position &gt; 1) {
                    var char = string_char_at(text, position), char_before = string_char_at(text, position - 1);
                    if (char == chr(10) || char_before == chr(10) || (string_lettersdigits_universal(char) != "" &amp;&amp; string_lettersdigits_universal(char_before) == "")) break;
                    --position;
                }
            text = string_delete(text, position, cursor_position - position); cursor_position = position;
            action = 2; break;
        }
    }
    
    // Text navigation with home/end and right/left.
    if (key_pressed == vk_home) {if (multiline &amp;&amp; !keyboard_check(vk_control)) cursor_position = new_line_list[| cursor_line] + 1; else cursor_position = 1; action = 3; break;}
    if (key_pressed == vk_end) {
        if (multiline &amp;&amp; !keyboard_check(vk_control)) {
            cursor_position = new_line_list[| cursor_line + 1]; if (cursor_position &lt;= string_length(text) &amp;&amp; string_char_at(text, cursor_position) != chr(10)) cursor_position *= -1;
        } else cursor_position = string_length(text) + 1; action = 3; break;
    }
    if (key_pressed == vk_right || key_pressed == vk_left) {
        do {
            if (key_pressed == vk_right) {if (cursor_position &gt; string_length(text)) break; ++cursor_position;}
            else {if (cursor_position == 1) break; --cursor_position;}
            var char = string_char_at(text, cursor_position), char_before = string_char_at(text, cursor_position - 1);
        } until (!keyboard_check(vk_control) || char == chr(10) || char_before == chr(10) || (string_lettersdigits_universal(char) != "" &amp;&amp; string_lettersdigits_universal(char_before) == ""));
        action = 3; break;
    }
    
    // Removes the ""-characters that keyboard_string gets when you do Ctrl + Backspace. Then adds the characters to text.
    if (keyboard_check(vk_control)) keyboard_string = ""; else keyboard_string = string_replace_all(keyboard_string, "", "");
    if (keyboard_string != "") {add_to_text = keyboard_string; action = 1; break;}
    
    break;
}

if (action) {
    keyboard_string = "";

    // Deletes characters in text selection (if enabled). Adds new characters. Limits text if too long.
    if (action != 3) {
        if (selection_enabled &amp;&amp; selection_start != 0) {
            text = string_delete(text, selection_start_ordered, selection_end_ordered - selection_start_ordered);
            cursor_position = selection_start_ordered; selection_start = 0; cursor_stay_visible = true;
        }
        if (add_to_text != "") {text = string_insert(add_to_text, text, cursor_position); cursor_position += string_length(add_to_text);}
        if (max_length != -1 &amp;&amp; string_length(text) &gt; max_length) {cursor_position -= string_length(text) - max_length; text = string_delete(text, cursor_position, string_length(text) - max_length);}
    }
    
    // Updates new_line_list and text_draw (possibly text too) when needed. (new_line_list contains the string positions for all chr(10)s and a beginning and an end. It'll also automatically add chr(10)s when text wrapping.)
    if (multiline) {
        if (action != 3) {
            while (true) {
                text_draw = text;
                ds_list_clear(new_line_list); ds_list_add(new_line_list, 0);
                var position = 0, delta_position, offset = 0;
                while (position &lt;= string_length(text)) {
                    delta_position = string_pos(chr(10), string_delete(text, 1, position)); if (!delta_position) delta_position = string_length(text) + 1 - position;
                    if (wrapping_width != -1) {
                        var position_1 = 0, delta_position, text_line = string_copy(text, position + 1, delta_position - 1), searched_width = 0, t;
                        if (string_width(string_replace_all(text_line, "#", "\#")) &gt; wrapping_width) {
                            while (true) {
                                delta_position_1 = string_pos(" ", string_delete(text_line, 1, position_1));
                                if (!delta_position_1 || string_width(string_replace_all(string_copy(text_line, 1, position_1 + delta_position_1 - 1), "#", "\#")) &gt; wrapping_width) break;
                                position_1 += delta_position_1;
                            }
                            if (h_align == fa_right &amp;&amp; position_1 == 1 &amp;&amp; string_char_at(text_line, 1) == " ") position_1 = 0;
                            if (position_1 == 0) while (true) {
                                ++position_1; searched_width += string_width(string_replace(string_char_at(text_line, position_1), "#", "\#"));
                                if (searched_width &gt; wrapping_width) {--position_1; break;}
                            }
                            if (position_1 == 0 &amp;&amp; delta_position != 2) position_1 = 1;
                            if (position_1 != 0) {
                                delta_position = position_1 - (h_align == fa_right &amp;&amp; string_char_at(text_line, position_1) == " " &amp;&amp; position_1 != 1);
                                ++offset; text_draw = string_insert(chr(10), text_draw, position + delta_position + offset);
                            }
                        }
                    }
                    position += delta_position; ds_list_add(new_line_list, position);
                }
                lines = ds_list_size(new_line_list) - 1;
                var i = 1, t = abs(cursor_position); while (t &gt; new_line_list[| i]) ++i; cursor_line = i - 1;
                if (max_lines != -1 &amp;&amp; lines &gt; max_lines) {
                    if (lines &gt; max_lines + 1) {var t = new_line_list[| cursor_line + max_lines + 2 - lines]; text = string_delete(text, t, cursor_position - t); cursor_position = t;}
                    else {--cursor_position; text = string_delete(text, cursor_position, 1);}
                } else break;
            }
            text_draw = string_replace_all(text_draw, "#", "\#");
        } else {var i = 1, t = abs(cursor_position); while (t &gt; new_line_list[| i]) ++i; cursor_line = i - 1;}
    } else text_draw = string_replace_all(text, "#", "\#");
    
    // Updates the vertical alignment offset, the cursor's display position, and visually fits the text inside the input area.
    var text_height_above_cursor, text_width_left_of_cursor, line_width;
    text_width = string_width(text_draw);
    if (multiline) {
        text_height = line_separation * (lines - 1) + char_height; text_height_above_cursor = line_separation * cursor_line;
        line_width = string_width(string_replace_all(string_copy(text, new_line_list[| cursor_line] + 1, new_line_list[| cursor_line + 1] - new_line_list[| cursor_line]), "#", "\#"));
        if (cursor_position &lt; 0) text_width_left_of_cursor = line_width;
        else text_width_left_of_cursor = string_width(string_replace_all(string_copy(text, new_line_list[| cursor_line] + 1, cursor_position - new_line_list[| cursor_line] - 1), "#", "\#"));
    } else {
        text_height = char_height; text_height_above_cursor = 0;
        text_width_left_of_cursor = string_width(string_replace_all(string_copy(text, 1, cursor_position - 1), "#", "\#"));
        line_width = text_width;
    }
    v_align_offset = 0;
    cursor_offset_x = text_width_left_of_cursor;
    if (h_align == fa_center) cursor_offset_x -= (line_width &gt;&gt; 1); else if (h_align == fa_right) cursor_offset_x -= line_width;
    if (v_align == fa_middle) v_align_offset -= (text_height &gt;&gt; 1); else if (v_align == fa_bottom) v_align_offset -= text_height;
    cursor_offset_y = text_height_above_cursor + v_align_offset;
    var local_scale_x_previous = local_scale_x, local_scale_y_previous = local_scale_y;
    if (fit_style == 0) {
        offset_x = clamp(offset_x, -origin_x - cursor_offset_x, -origin_x + width - cursor_offset_x);
        offset_y = clamp(offset_y, -origin_y - cursor_offset_y, -origin_y + height - cursor_offset_y - char_height);
        
        if (h_align == fa_left) offset_x = clamp2(offset_x, width - text_width, 0);
        else if (h_align == fa_right) offset_x = clamp2(offset_x, 0, text_width - width);
        else offset_x = clamp2(offset_x, (width - text_width) &gt;&gt; 1, (text_width - width) &gt;&gt; 1);
        
        if (v_align == fa_top) offset_y = clamp2(offset_y, height - text_height, 0);
        else if (v_align == fa_bottom) offset_y = clamp2(offset_y, 0, text_height - height);
        else offset_y = clamp2(offset_y, (height - text_height) &gt;&gt; 1, (text_height - height) &gt;&gt; 1);
        
        cursor_offset_x += offset_x; cursor_offset_y += offset_y;
    } else {
        if (text_width &lt;= width) local_scale_x = 1; else local_scale_x = width / text_width;
        if (text_height &lt;= height) local_scale_y = 1; else local_scale_y = height / text_height;
        if (fit_style == 1) {local_scale_x = min(local_scale_x, local_scale_y); local_scale_y = local_scale_x;}
        matrix_local = matrix_build(origin_x, origin_y, 0, 0, 0, 0, local_scale_x, local_scale_y, 1);
        offset_x = 0; offset_y = 0;
    }
    if (local_scale_x != local_scale_x_previous || local_scale_y != local_scale_y_previous) {
        image_xscale = global_scale_x * local_scale_x; image_yscale = global_scale_y * local_scale_y;
        if (os_browser == browser_not_a_browser) {
            matrix_cursor = matrix_build(0, 0, 0, 0, 0, 0, image_xscale, image_yscale, 1); matrix_cursor = matrix_multiply(matrix_cursor, matrix_build(x, y, 0, 0, 0, image_angle, 1, 1, 1));
        }
    }
    if (os_browser != browser_not_a_browser) {
        var t_x = cursor_offset_x * image_xscale, t_y = cursor_offset_y * image_yscale;
        cursor_x = round(x + lengthdir_x(t_x, image_angle) - lengthdir_y(t_y, image_angle));
        cursor_y = round(y + lengthdir_x(t_y, image_angle) + lengthdir_y(t_x, image_angle));
    }
    
    // If a certain change in action was recognized this step, this saves the previous text and cursor position to the undo/redo list.
    if (max_undo_redo_states != 0 &amp;&amp; action != action_previous) {
        if (!(action == 3 &amp;&amp; action_previous == 5) &amp;&amp; !(action == 5 &amp;&amp; action_previous == 3)) {
            var last;
            if (action == 5) --undo_redo_position;
            else while (true) {
                last = ds_list_size(undo_redo_text_list) - 1;
                if (undo_redo_position == last + 1) break;
                ds_list_delete(undo_redo_text_list, last); ds_list_delete(undo_redo_cursor_position_list, last);
            }
            ds_list_add(undo_redo_text_list, text_previous); ds_list_add(undo_redo_cursor_position_list, cursor_position_previous);
            if (max_undo_redo_states != -1) while (ds_list_size(undo_redo_text_list) &gt; max_undo_redo_states) {
                ds_list_delete(undo_redo_text_list, 0); ds_list_delete(undo_redo_cursor_position_list, 0);
                --undo_redo_position;
            }
            ++undo_redo_position;
        }
        action_previous = action;
    }
    
    // If text selection is happening.
    if (selection_enabled) {
        if (action == 3 &amp;&amp; (keyboard_check(vk_shift) || (mouse_check_button(mb_left) &amp;&amp; !mouse_check_button_pressed(mb_left))) || selection_update) {
            var i;
            if (selection_start == 0 || selection_update) {
                if (!selection_update) selection_start = cursor_position_previous;
                if (multiline) {
                    if (selection_start &lt; 0) selection_start = -selection_start + 1;
                    i = 0; while (selection_start &gt; new_line_list[| i]) ++i; selection_start_line = i - 1;
                } else selection_start_line = 0;
            }
            selection_end = cursor_position;
            if (multiline) {
                if (selection_end &lt; 0) selection_end = -selection_end + 1;
                i = 0; while (selection_end &gt; new_line_list[| i]) ++i; selection_end_line = i - 1;
            } else selection_end_line = 0;
            if (selection_start == selection_end) {
                selection_start = 0;
            } else if (selection_start &lt; selection_end) {
                selection_start_ordered = selection_start; selection_end_ordered = selection_end;
                selection_start_line_ordered = selection_start_line; selection_end_line_ordered = selection_end_line;
            } else {
                selection_start_ordered = selection_end; selection_end_ordered = selection_start;
                selection_start_line_ordered = selection_end_line; selection_end_line_ordered = selection_start_line;
            }
        } else if (selection_start != 0) {selection_start = 0; cursor_stay_visible = true;}
    }
    
    if (cursor_position != cursor_position_previous) {cursor_stay_visible = true; cursor_position_previous = cursor_position;}
    
    // Most text editors allow for two different visualizations of the cursor when it's between text wrapped lines; either end of first line, or beginning of second line. A negative cursor position indicates end of line.
    if (multiline &amp;&amp; cursor_position &lt; 0) cursor_position = -cursor_position + 1;
    
    update_surfaces = true;
}

if (enabled) {
    ++enabled_step;
    // Makes the cursor blink.
    cursor_timer -= delta_time; if (cursor_timer &lt;= -cursor_time || cursor_stay_visible) cursor_timer = cursor_time;
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="64">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// Updates surfaces. (Surfaces are needed to allow for custom text selection coloring, and to only show certain parts of the whole text when it's too long. Might in some cases be faster as well.)
if (!surface_exists(sf_main) || (selection_enabled &amp;&amp; !surface_exists(sf_selection))) update_surfaces = true;
if (update_surfaces) {
    update_surfaces = false;
    draw_set_font(font);
    matrix_set(matrix_world, matrix_local);
    
    // Draws the text selection to its own surface.
    if (selection_enabled) {
        if (!surface_exists(sf_selection)) sf_selection = surface_create(width + eoln_width, height);
        if (selection_start != 0) {
            surface_set_target(sf_selection);
                draw_clear_alpha(c_black, 0); draw_set_color(c_white);
                var i, first, last, selection_start_x, selection_end_x, selection_start_y, line_width, cover_x = -1 + draw_rectangle_add, cover_y = max(line_separation, char_height) - 1 + draw_rectangle_add;
                for (i = selection_start_line_ordered; i &lt;= selection_end_line_ordered; ++i) {
                    if (multiline) {
                        line_width = string_width(string_replace_all(string_copy(text, new_line_list[| i] + 1, new_line_list[| i + 1] - new_line_list[| i]), "#", "\#"));
                        if (i == selection_start_line_ordered) {
                            first = selection_start_ordered; selection_start_x = string_width(string_replace_all(string_copy(text, new_line_list[| i] + 1, first - new_line_list[| i] - 1), "#", "\#"));
                        } else {first = new_line_list[| i] + 1; selection_start_x = 0;}
                    } else {line_width = text_width; first = selection_start_ordered; selection_start_x = string_width(string_replace_all(string_copy(text, 1, first - 1), "#", "\#"));}
                    selection_start_x += offset_x; if (h_align == fa_center) selection_start_x -= line_width &gt;&gt; 1; else if (h_align == fa_right) selection_start_x -= line_width;
                    if (i == selection_end_line_ordered) last = selection_end_ordered - 1; else last = new_line_list[| i + 1];
                    if (i == selection_start_line_ordered || i == selection_end_line_ordered) selection_end_x = selection_start_x + string_width(string_replace_all(string_copy(text, first, last - first + 1), "#", "\#"));
                    else selection_end_x = selection_start_x + line_width;
                    if (i &lt; selection_end_line_ordered &amp;&amp; string_char_at(text, last) == chr(10)) selection_end_x += eoln_width;
                    if (selection_start_x != selection_end_x) {
                        selection_start_y = offset_y + v_align_offset + line_separation * i;
                        draw_rectangle(selection_start_x, selection_start_y, selection_end_x + cover_x, selection_start_y + cover_y, false);
                    }
                }
            surface_reset_target();
        }
    } 
    
    // Draws the text to the main surface. (Since GM has a bug when vertically aligning certain characters, we stick to fa_top and find the vertical alignment offset manually in the step event.)
    if (!surface_exists(sf_main)) sf_main = surface_create(width + eoln_width, height);
    surface_set_target(sf_main);
        draw_enable_alphablend(false); draw_set_alpha_test(true); draw_set_alpha_test_ref_value(0);
        draw_clear_alpha(c_black, 0);
        draw_set_halign(h_align); draw_set_valign(fa_top); draw_set_color(color); draw_set_alpha(alpha);
        draw_text_ext(offset_x, offset_y + v_align_offset, text_draw, line_separation, -1);
        draw_set_alpha(1);
        draw_set_alpha_test(false); draw_enable_alphablend(true);
    surface_reset_target();
}

matrix_set(matrix_world, matrix_global);
if (selection_enabled &amp;&amp; selection_start != 0) {
    if (shaders) {
        shader_set(sh_input_text_selection);
        texture_set_stage(uniform_sf_selection, surface_get_texture(sf_selection));
        shader_set_uniform_f(uniform_selection_color_text, selection_red_text, selection_green_text, selection_blue_text, selection_alpha_text);
        shader_set_uniform_f(uniform_selection_color_background, selection_red_background, selection_green_background, selection_blue_background, selection_alpha_background);
        shader_set_uniform_f(uniform_alpha, alpha);
        draw_surface(sf_selection, -origin_x, -origin_y)
    } else draw_surface_ext(sf_selection, -origin_x, -origin_y, 1, 1, 0, selection_color_background, selection_alpha_background);
}
draw_surface(sf_main, -origin_x, -origin_y);
if (shaders &amp;&amp; selection_enabled &amp;&amp; selection_start != 0) shader_reset();

// Draws the cursor in the given style.
if (enabled &amp;&amp; (!selection_enabled || selection_start == 0)) {
    var cursor_alpha;
    switch (cursor_style) {
        case 0: cursor_alpha = (cursor_timer &gt; 0); break;
        case 1: cursor_alpha = sqr(max(0, cursor_timer / cursor_time + 0.5)); break;
        case 2: cursor_alpha = sqr(abs(cursor_timer / cursor_time)); break;
    }
    if (cursor_alpha &gt; 0) {
        if (os_browser == browser_not_a_browser) {matrix_set(matrix_world, matrix_cursor); draw_sprite_ext(cursor, 0, cursor_offset_x, cursor_offset_y, cursor_scale, cursor_scale, 0, color, cursor_alpha);}
        else {matrix_set(matrix_world, matrix_identity); draw_sprite_ext(cursor, 0, cursor_x, cursor_y, cursor_scale * image_xscale, cursor_scale * image_yscale, image_angle, color, cursor_alpha);}
    }
}

matrix_set(matrix_world, matrix_identity);
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
